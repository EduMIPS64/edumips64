/*
 * EduMips64 Gradle build configuration
 */

plugins {
    id 'java'
    id 'application'
    id "kr.motd.sphinx" version "2.3.1"                     // Sphinx plugin: to build the documentation   
    id "com.github.johnrengelman.shadow" version "4.0.3"    // Shadow plugin: to build the uber/fat/standalone jar
}

repositories {
    jcenter()
}

dependencies {
    implementation 'com.google.gwt:gwt-user:2.8.2'
    implementation 'javax.help:javahelp:2.0.05'
    testImplementation 'junit:junit:4.12'
}

mainClassName = 'org.edumips64.Main'
version = "1.2.5"

/*
 * Fat / standalone jar tasks
 */
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
   classifier = 'standalone'
}

// CLI standalone jar task
// https://github.com/johnrengelman/shadow/issues/138#issuecomment-446238613
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task cliJar(type: ShadowJar, dependsOn: shadowJar) {
  mergeServiceFiles()
  classifier = 'cli'
  manifest {
        attributes 'Main-Class': 'org.edumips64.MainCLI'
  }
  group = 'build'
  // TODO: remove GUI-related source files from sourceSet of CLI jar
  from zipTree(shadowJar.archivePath)
  description = 'Assemble the CLI jar file'
  configurations = [project.configurations.runtime]
}
assemble.dependsOn(cliJar)  // make sure to execute cliJar upon assemble


/* 
 * Documentation tasks
 */
task itHtmlDocs (type: kr.motd.gradle.sphinx.gradle.SphinxTask){
    builder = "html"
    sourceDirectory = "${projectDir}/docs/user/it/src"
    outputDirectory = "build/docs/it/html"
    group = 'Documentation'
    description = 'Compile the HTML documentation in Italian'
}
task itLatexDocs (type: kr.motd.gradle.sphinx.gradle.SphinxTask){
    builder = "latex"
    sourceDirectory = "${projectDir}/docs/user/it/src"
    outputDirectory = "build/docs/it/latex"
    group = 'Documentation'
    description = 'Compile the LateX documentation in Italian'
}
task enHtmlDocs (type: kr.motd.gradle.sphinx.gradle.SphinxTask){
    builder = "html"
    sourceDirectory = "${projectDir}/docs/user/en/src"
    outputDirectory = "build/docs/en/html"
    group = 'Documentation'
    description = 'Compile the HTML documentation in English'
}
task enLatexDocs (type: kr.motd.gradle.sphinx.gradle.SphinxTask){
    builder = "latex"
    sourceDirectory = "${projectDir}/docs/user/en/src"
    outputDirectory = "build/docs/en/latex"
    group = 'Documentation'
    description = 'Compile the LateX documentation in English'
}
// Catch-all task for documentation
task allDocs(type: GradleBuild) {
     tasks = ['itHtmlDocs', 'itLatexDocs', 'enHtmlDocs', 'enLatexDocs']
     description = 'Run all documentation tasks'
}