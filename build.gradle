/*
 * EduMips64 Gradle build configuration
 */

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.3"    // Shadow plugin: to build the uber/fat/standalone jar
}

repositories {
    jcenter()
}

dependencies {
    implementation 'com.google.gwt:gwt-user:2.8.2'
    implementation 'javax.help:javahelp:2.0.05'
    testImplementation 'junit:junit:4.12'
}

mainClassName = 'org.edumips64.Main'
version = '1.2.5'
ext.codename = 'Eden'


/* 
 * Documentation tasks
 */
task htmlDocsIt(type: Exec) {
    workingDir "${projectDir}/docs/user/en/src"
    commandLine "make", "html", "BUILDDIR=${buildDir}/docs/en", "SPHINXOPTS=-N -a -E -Q"
}
task htmlDocsEn(type: Exec) {
    workingDir "${projectDir}/docs/user/it/src"
    commandLine "make", "html", "BUILDDIR=${buildDir}/docs/it", "SPHINXOPTS=-N -a -E -Q"
}
task pdfDocsEn(type: Exec) {
    workingDir "${projectDir}/docs/user/en/src"
    commandLine "make", "latexpdf", "BUILDDIR=${buildDir}/docs/en", "LATEXMKOPTS=-xelatex", "SPHINXOPTS=-N -a -E -Q"
}
task pdfDocsIt(type: Exec) {
    workingDir "${projectDir}/docs/user/it/src"
    commandLine "make", "latexpdf", "BUILDDIR=${buildDir}/docs/it", "LATEXMKOPTS=-xelatex", "SPHINXOPTS=-N -a -E -Q"
}
// Catch-all task for documentation
task allDocs(type: GradleBuild) {
    tasks = ['htmlDocsIt', 'htmlDocsEn', 'pdfDocsEn', 'pdfDocsIt']
    description = 'Run all documentation tasks'
}
// Include allDocs task in check task
check.dependsOn(allDocs)


/*
 * Jar tasks
 */
// Compile HTML docs for JavaHelp before assembling the jar
jar.dependsOn htmlDocsIt
jar.dependsOn htmlDocsEn

// Include the docs folder at the root of the jar, for JavaHelp
processResources {
    from ("${buildDir}/docs/en") {
        into 'docs/user/en'
        includes = ["html/**"]
        excludes = ["**/_sources/**"]
    }
    from ("${buildDir}/docs/it") {
        into 'docs/user/it'
        includes = ["html/**"]
        excludes = ["**/_sources/**"]
    }
    from ('docs/') {
        into 'docs'
        excludes = ["**/src/**", "**/design/**", "**/*.py", "**/*.md"]
    }
}

def getGitRevisionId() {
  def branch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
  def commitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()
  return 'edumips64:' + branch + ':' + commitHash
}

ext.sharedManifest = manifest {
    attributes(
        'Signature-Version': version,
        'Codename': codename,
        'Build-Date': new Date(),
        'Git-Revision': getGitRevisionId() )
}

// Main "slim/nodeps" jar
jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'SplashScreen-Image': 'images/splash.png'
        from sharedManifest        
    }
}

// Cli jar
task cliJar(type: Jar, dependsOn: jar) {  
    classifier = 'cli'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'org.edumips64.MainCLI'
        from sharedManifest        
    }
}
assemble.dependsOn(cliJar) 

// TODO check out this https://docs.gradle.org/current/userguide/working_with_files.html#sec:creating_uber_jar_example
// "Fat" / standalone jar (includes all dependencies)
shadowJar {
   classifier = 'standalone'
}
